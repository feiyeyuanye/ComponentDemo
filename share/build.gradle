// 注意这里
if(isRunAlone.toBoolean()){
    apply plugin: 'com.android.application'
}else{
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion compile_sdk_version.toInteger()
    // 组件的资源隔离
    resourcePrefix "share_"
    defaultConfig {
        // 注意这里
        if(isRunAlone.toBoolean()){
            // 单独调试时添加 applicationId ，集成调试时移除
            applicationId "com.example.share"
        }
        minSdkVersion min_sdk_version.toInteger()
        targetSdkVersion target_sdk_version.toInteger()
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // alibaba Arouter
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }
    }
    // 注意这里
    sourceSets {
        main {
            // 单独调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (isRunAlone.toBoolean()) {
                // 单独调试
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                // 集成调试
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation project (':base')
    // alibaba Arouter
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

}
